plugins {
    id 'net.minecraftforge.gradle' version '6.0.+'
    id 'com.github.johnrengelman.shadow' version '8.1.+'
    id 'wtf.gofancy.fancygradle' version '1.1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

fancyGradle {
    patches {
        resources
        coremods
        asm
        mergetool
    }
}

version = "${project.minecraft_version}-${project.mod_version}"
group = project.maven_group
archivesBaseName = project.base_name

java.toolchain.getLanguageVersion().set(JavaLanguageVersion.of(8))

minecraft {
    mappings channel: 'stable', version: mappings_version
    runs {
        client {
            workingDirectory(file('run'))
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

repositories {
    maven {
        name = 'BlameJared'
        url = uri('https://maven.blamejared.com')
    }
    maven {
        name = 'CurseMaven'
        url = uri('https://www.cursemaven.com')
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    //minecraft
    minecraft "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    //required mod dependencies
    compileOnly(fg.deobf("CraftTweaker2:CraftTweaker2-API:${project.ct_version}"))
    compileOnly(fg.deobf("CraftTweaker2:ZenScript:${project.ct_version}"))
    compileOnly(fg.deobf("curse.maven:bookshelf-228525:${project.bookshelf_version}"))
    compileOnly(fg.deobf("curse.maven:game-stages-268655:${project.gamestages_version}"))
    compileOnly(fg.deobf("curse.maven:journeymap-32274:${project.journeymap_version}"))
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    inputs.property 'version', project.version
    inputs.property 'mcversion', minecraft_version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': minecraft_version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes([
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}